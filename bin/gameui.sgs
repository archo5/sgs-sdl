

global _efn = function(){};

global _ui_control_iface =
{
	/* references */
	parent = null,
	
	/* events */
	onclick = _efn,
	onhittest = function( x, y )
	{
		return this.x <= x && this.x + this.w >= x
			&& this.y <= y && this.y + this.h >= y;
	},
	onenter = function(){ this.mouseon = true; },
	onleave = function(){ this.mouseon = false; },
	onmousedown = function(){ if( this.mouseon ) this.clicked = true; },
	onmouseup = function(){ if( this.mouseon ) this.onclick(); this.clicked = false; },
	
	/* states */
	clicked = false,
	mouseon = false,
	
	/* processing */
	get_ctrl_at = function( x, y )
	{
		for( var i = this.children.size - 1; i >= 0; --i )
		{
			var sub = this.children[ i ].get_ctrl_at( x, y );
			if( sub !== null )
				return sub;
		}
		if( this.onhittest( x, y ) )
			return this;
		return null;
	},
	add_child = function( ch ){ ch.parent = this; this.children.push( ch ); },
	on_draw = null,
};

function ui_create_control( override )
{
	var data = { children = [] };
	foreach( i : override )
		data[ i ] = override[ i ];
	return class( data, _ui_control_iface );
}

function ui_default_paint_button()
{
	var btnbgc = [ 0.1 + this.mouseon * 0.7, 0.1 + this.clicked * 0.7, 0.1, 0.5 ];
	draw({ preset = 'tile', position = [this.x,this.y], scale = [this.w,this.h], color = btnbgc });
}

function ui_create_button( name, x, y, w, h, onclick )
{
	return ui_create_control
	({
		name = name,
		type = "button",
		x = x,
		y = y,
		w = w,
		h = h,
		onclick = onclick,
		on_draw = ui_default_paint_button
	});
}


global _ui_frame_iface =
{
	name = "__frame__",
	type = "frame",
	x = 0,
	y = 0,
	w = 9999,
	h = 9999,
	
	curhover = null,
	curclick = null,
	
	event = function( e )
	{
		if( e.type == SDL_MOUSEMOTION )
		{
			var ctrl = this.get_ctrl_at( e.x, e.y );
			if( ctrl !== this.curhover )
			{
				if( this.curhover !== null )
					this.curhover.onleave();
				this.curhover = ctrl;
				ctrl.onenter();
			}
		}
		if( e.type == SDL_MOUSEBUTTONDOWN || e.type == SDL_MOUSEBUTTONUP )
		{
			var ctrl = this.get_ctrl_at( e.x, e.y );
			if( e.type == SDL_MOUSEBUTTONDOWN )
			{
				ctrl.onmousedown( e.x, e.y, e.button );
				this.curclick = ctrl;
			}
			else
			{
				ctrl.onmouseup( e.x, e.y, e.button );
				if( this.curclick !== null && ctrl !== this.curclick )
					this.curclick.onmouseup( e.x, e.y, e.button );
				this.curclick = null;
			}
		}
	},
	draw = function()
	{
		foreach( idx : this.children )
		{
			if( this.children[ idx ].on_draw )
				this.children[ idx ].on_draw();
		}
	}
};

function ui_create_frame()
{
	return ui_create_control( _ui_frame_iface );
}
