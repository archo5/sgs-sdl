
#ifdef VS

float4x4 mWorldView : register(c0);
float4x4 mProj : register(c4);

#ifdef SKIN
float4x4 mSkin[32] : register(c40); /* :c167 */
#endif

void main
(
	float4 ipos : POSITION0,
	float3 inrm : NORMAL0,
	float4 itan : TANGENT0,
	float4 icol : COLOR0,
#ifdef SKIN
	float4 iwts : BLENDWEIGHT0,
	int4 idcs : BLENDINDICES0,
#endif
	float4 itex0 : TEXCOORD0,
	float4 itex1 : TEXCOORD1,
	out float4 opos : POSITION0,
	out float4 otfpos : TEXCOORD4,
	out float4 oT : TEXCOORD6,
	out float3 oN : TEXCOORD7,
	out float4 ocol : COLOR0,
	out float4 otex0 : TEXCOORD0,
	out float4 otex1 : TEXCOORD1
)
{
#ifdef SKIN
	ipos =
		mul( mSkin[ idcs[0] ], ipos ) * iwts[0] +
		mul( mSkin[ idcs[1] ], ipos ) * iwts[1] +
		mul( mSkin[ idcs[2] ], ipos ) * iwts[2] +
		mul( mSkin[ idcs[3] ], ipos ) * iwts[3]
	;
	itan.xyz =
		mul( mSkin[ idcs[0] ], itan.xyz ) * iwts[0] +
		mul( mSkin[ idcs[1] ], itan.xyz ) * iwts[1] +
		mul( mSkin[ idcs[2] ], itan.xyz ) * iwts[2] +
		mul( mSkin[ idcs[3] ], itan.xyz ) * iwts[3]
	;
	inrm =
		mul( mSkin[ idcs[0] ], inrm ) * iwts[0] +
		mul( mSkin[ idcs[1] ], inrm ) * iwts[1] +
		mul( mSkin[ idcs[2] ], inrm ) * iwts[2] +
		mul( mSkin[ idcs[3] ], inrm ) * iwts[3]
	;
#endif
	
	float4 viewPos = mul( mWorldView, ipos );
	opos = mul( mProj, viewPos );
	otfpos = viewPos;
	oT = float4( mul( mWorldView, float4( itan.xyz, 0 ) ).xyz, itan.w );
	oN = mul( mWorldView, float4( inrm, 0 ) ).xyz;
	ocol = icol;
	otex0 = itex0;
	otex1 = itex1;
}

#elif defined(PS)

sampler2D Tex0 : register(s0);
sampler2D Tex1 : register(s1);
sampler2D Tex2 : register(s2);
sampler2D Tex3 : register(s3);
sampler2D Tex4 : register(s4);
sampler2D Tex5 : register(s5);
sampler2D Tex6 : register(s6);
sampler2D Tex7 : register(s7);
samplerCUBE InstTex0 : register(s8);
samplerCUBE InstTex1 : register(s9);
samplerCUBE InstTex2 : register(s10);
samplerCUBE InstTex3 : register(s11);

float4x4 mInvView : register(c0);
float4x4 mProj : register(c4);
float3 gAmbLightColor : register(c20);
float3 gDirLightDir : register(c21);
float3 gDirLightColor : register(c22);
struct PointLight /* size: 2c */
{
	float4 vposrad;
	float4 colpow;
};
float4 gLightCounts : register(c23);
PointLight gPLights[16] : register(c56); /* :c87 */

float4 gInstanceData[16] : register(c100); /* :c115 */

void main
(
	float4 inViewPos : TEXCOORD4,
	float4 inTangentSign : TEXCOORD6,
	float3 inNormal : TEXCOORD7,
	float4 icol : COLOR0,
	float4 inTex0 : TEXCOORD0,
	float4 inTex1 : TEXCOORD1,
	out float4 _outColor0 : COLOR0,
	out float4 _outColor1 : COLOR1
)
{
	float RdotV, specAmount;
	
	float3 viewPos = inViewPos.xyz;
	float4 projPos = mul( mProj, inViewPos );
	float3 viewDir = normalize( viewPos );
	
	float3 T = inTangentSign.xyz;
	float3 B = cross( inNormal, inTangentSign.xyz ) * inTangentSign.w;
	float3 N = inNormal;
	
	float3x3 TBN = float3x3( T, B, N );
	
	float3 outDiffuseColor = float3( 0.7, 0.7, 0.7 );
	float outOpacity = 1.0;
	float3 outSpecularColor = float3( 0.3, 0.3, 0.3 );
	float outSpecularPower = 32;
	float3 outNormal = inNormal;
	float2 outDistortion = float2( 0, 0 );
	float3 outAmbientDiffuseLight = float3( 0, 0, 0 );
	float3 outAmbientSpecularLight = float3( 0, 0, 0 );
	float3 outEmissiveLight = float3( 0, 0, 0 );
	{
__CODE__
	}
	outNormal = normalize( outNormal );
	
	float3 totalDiffuse = float3(0,0,0);
	float3 totalSpecular = float3(0,0,0);
	
	/* POINT LIGHTS */
	for( int i = 0; i < gLightCounts.x; ++i )
	{
		PointLight PL = gPLights[ i ];
		float3 lightViewPos = PL.vposrad.xyz;
		float3 lightDir = lightViewPos - viewPos;
		float lightDist = length( lightDir );
		lightDir /= lightDist;
		float3 lightColor = PL.colpow.rgb;
		
		float distFactor = pow( 1 - saturate( lightDist / PL.vposrad.w ), PL.colpow.w );
		float NdotL = saturate( dot( outNormal, lightDir ) );
		
		RdotV = saturate( dot( reflect( lightDir, outNormal ), viewDir ) );
		specAmount = pow( RdotV, outSpecularPower ) * sqrt( outSpecularPower );
		
		totalDiffuse += lightColor * NdotL * distFactor;
		totalSpecular += lightColor * specAmount * distFactor;
	}
	
	_outColor0 = float4( totalDiffuse * outDiffuseColor + totalSpecular * outSpecularColor, outOpacity );
	_outColor1 = float4( 0, 0, 0, 0 );
}

#endif

