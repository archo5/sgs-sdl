
global Histogram =
{
};

function Histogram.create( limit )
{
	limit ||= 100;
	data =
	{
		limit = limit,
		data = [],
		vfmt = make_vertex_format( "pf2cc4" ),
		rb = create_renderbuf(),
	};
	return class( data, Histogram );
}

function Histogram.push( val )
{
	while( this.data.size >= this.limit )
		this.data.shift();
	this.data.push( val );
}

function Histogram.draw( x, y, xscale, yscale, color, vlim )
{
	bc = 0x7f000000;
	
	bc = d3dcolor_fix( bc );
	color = d3dcolor_fix( color );
	
	bg = fmt_pack( "ffl ffl ffl ffl", x, y, bc, x + this.limit * xscale, y, bc,
		x + this.limit * xscale, y + yscale * vlim, bc, x, y + yscale * vlim, bc );
	draw_packed( null, this.vfmt, bg, 0, 4, PT_QUADS );
	
	rb = this.rb.begin();
	foreach( px, py : this.data )
	{
		rb.f( x + px * xscale, y + py * yscale ).dw( color );
	}
	if( this.data.size )
		rb.draw( null, this.vfmt, 0, this.data.size, PT_LINE_STRIP );
}
