
include_file( "engine/font.sgs" );
include_file( "engine/draw.sgs" );


global _efn = function(){};


global UIControl =
{
	/* references */
	parent = null,
	
	/* states */
	clicked = false,
	mouseon = false,
	
	/* processing */
	on_draw = null,
};

/* DEFAULT events */
UIControl.onclick = _efn;
function UIControl.onhittest( x, y )
{
	return this.x <= x && this.x + this.w >= x
		&& this.y <= y && this.y + this.h >= y;
}
function UIControl.onenter(){ this.mouseon = true; }
function UIControl.onleave(){ this.mouseon = false; }
function UIControl.onmousedown( x, y, bid ){ if( this.mouseon && bid == 1 ) this.clicked = true; }
function UIControl.onmouseup(){ if( this.clicked && this.mouseon ) this.onclick(); this.clicked = false; }

/* PROCESSING */
function UIControl.get_ctrl_at( x, y )
{
	for( var i = this.children.size - 1; i >= 0; --i )
	{
		var sub = this.children[ i ].get_ctrl_at( x, y );
		if( sub !== null )
			return sub;
	}
	if( this.onhittest( x, y ) )
		return this;
	return null;
}
function UIControl.addChild( ch )
{
	ch.parent = this;
	this.children.push( ch );
	return ch;
}

function UIControl.create( override )
{
	var data = { children = [] };
	data.font = create_font( "verdana.ttf", 12 );
	foreach( k, v : override )
		data[ k ] = v;
	return class( data, UIControl );
}


function ui_default_paint_button()
{
	mon = this.mouseon; if( this.clicked ) mon = 0;
	tex = this.textures[ mon + this.clicked * 2 ];
	draw_button( tex, this.x, this.x + this.w, this.y, this.y + this.h );

	draw_text_rect( this.name, this.font, [0.9,1], DT_CENTER | DT_VCENTER,
		this.x + 8, this.x + this.w - 8, this.y + 9, this.y + this.h - 7 );
	draw_text_rect( this.name, this.font, [0.1,1], DT_CENTER | DT_VCENTER,
		this.x + 8, this.x + this.w - 8, this.y + 8, this.y + this.h - 8 );
}

function UIControl.createButton( name, x, y, w, h, onclick )
{
	return UIControl.create
	({
		name = name,
		type = "button",
		x = x,
		y = y,
		w = w,
		h = h,
		onclick = onclick,
		on_draw = ui_default_paint_button,
		textures =
		[
			create_texture( "engine/img/buttonN.png" ),
			create_texture( "engine/img/buttonH.png" ),
			create_texture( "engine/img/buttonC.png" ),
		],
	});
}



function ui_default_paint_menu()
{
	draw({ preset = "tile", position = [ this.x, this.y ],
		scale = [ this.w, this.h ], color = [ 0.8, 0.8, 0.8 ] });
	
	foreach( ch : this.children )
	{
		if( ch.on_draw )
			ch.on_draw();
	}
}

function UIControl.createMenu( x, y, w, h )
{
	function menuAddChild( ch )
	{
		UIControl.addChild.thiscall( this, ch );
		return ch;
	}

	return UIControl.create
	({
		type = "button",
		x = x,
		y = y,
		w = w,
		h = h,
		on_draw = ui_default_paint_menu,

		addChild = menuAddChild,
	});
}


global UIFrame =
{
	name = "__frame__",
	type = "frame",
	x = 0,
	y = 0,
	w = 9999,
	h = 9999,
	
	curhover = null,
	curclick = null,
};

function UIFrame.event( e )
{
	if( e.type == SDL_MOUSEMOTION )
	{
		var ctrl = this.get_ctrl_at( e.x, e.y );
		if( ctrl !== this.curhover )
		{
			if( this.curhover !== null )
				this.curhover.onleave();
			this.curhover = ctrl;
			ctrl.onenter();
		}
	}
	if( e.type == SDL_MOUSEBUTTONDOWN || e.type == SDL_MOUSEBUTTONUP )
	{
		var ctrl = this.get_ctrl_at( e.x, e.y );
		if( e.type == SDL_MOUSEBUTTONDOWN )
		{
			ctrl.onmousedown( e.x, e.y, e.button );
			this.curclick = ctrl;
		}
		else
		{
			ctrl.onmouseup( e.x, e.y, e.button );
			if( this.curclick !== null && ctrl !== this.curclick )
				this.curclick.onmouseup( e.x, e.y, e.button );
			this.curclick = null;
		}
	}
}

function UIFrame.draw()
{
	foreach( ch : this.children )
	{
		if( ch.on_draw )
			ch.on_draw();
	}
}

function UIFrame.create()
{
	return UIControl.create( UIFrame );
}
