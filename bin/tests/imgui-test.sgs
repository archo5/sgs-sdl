
include "sgsimgui", "core/ext_imgui";

function configure()
{
}

function initialize()
{
	global Window = SS_CreateWindow( "SGS-SDL Game Framework", SDL_WINDOWPOS_UNDEFINED, SDL_WINDOWPOS_UNDEFINED, 1024, 576, SDL_WINDOW_OPENGLMAYBE | SDL_WINDOW_RESIZABLE );
	Window.initRenderer( SS_RENDERER_DONTCARE, SS_RENDERER_VSYNC );
	global Font = SS_CreateFont( "fonts/lato-regular.ttf", 12 );
	SS_InitDrawFunctions();
	
	ImFontAtlas_AddFontFromFileTTF( "fonts/lato-regular.ttf", 14 );
	ImGui_SS_Init( Window.width, Window.height );
}

class ExampleItem
{
	function __construct()
	{
		this.intval = 5;
		this.realval = 3.14;
	}
	function OnGUI()
	{
		(c,this.intval) = ImGui_SliderInt( "Int value", this.intval, 0, 10 );
		(c,this.realval) = ImGui_SliderFloat( "Real value", this.realval, 0, 10, "%.2f", 1 );
	}
}

global lasttime = ftime();
global whichtest = 0;
global testnum = 1.5;
global testarr = [];
global testfilepicker = new ImGuiFilePicker( "tests", ".sgs" );
global testfile = "imgui-test.sgs";
function update()
{
	global whichtest;
	
	curtime = ftime();
	dt = curtime - lasttime;
	global lasttime = curtime;
	
	SS_Clear( color(0.1,0.2,0.4) );
	SS_SetCameraUI( 0, Window.width, 0, Window.height );
	
	ImGui_SS_NewFrame( dt );
	
	if( ImGui_IsKeyDown( SDL_SCANCODE_F1 ) ) whichtest = 0;
	if( ImGui_IsKeyDown( SDL_SCANCODE_F2 ) ) whichtest = 1;
	
	ImGui_Begin( "Info" );
	ImGui_Text( "F1 - basic test, F2 - editor layout test" );
	ImGui_End();
	
	if( whichtest == 0 )
	{
		ImGui_ShowTestWindow();
	}
	else if( whichtest == 1 )
	{
		ImGui_MainWindow( Window.width, Window.height, function()
		{
			ImGui_BeginMenuBar();
			if( ImGui_BeginMenu( "Editor" ) )
			{
				if( ImGui_MenuItem( "Quit" ) ) _G.whichtest = 0;
				ImGui_EndMenu();
			}
			ImGui_EndMenuBar();
			
			ImGui_HSplit3( 0.3, 0.7,
				function()
				{
					ImGui_Text( "part 1" );
					ImGui_EditArray( testarr, function(e){ e.OnGUI(); }, function(){ return new ExampleItem(); } );
				},
				function()
				{
					ImGui_Text( "part 2" );
					(x,y) = ImGui_GetCursorScreenPos();
					ImDrawList_AddCallback(function() use(x,y)
					{
						SS_DrawColorCircle( x + 32, y + 48, 24, 12, 1, 0, 0, 0.5 );
						SS_DrawTextLine_VC( "custom", Font, x + 16, y + 48, color(1,1,1) );
					});
					ImDrawList_AddCircle( x + 32, y + 48, 32, ImGui_GetColorU32( 1, 1, 1, 0.5 ), 32, 5 );
				},
				function()
				{
					ImGui_Text( "part 3" );
					(chg,_G.testnum) = ImGui_SliderFloat( "test number", _G.testnum, 0, 10, "%.2f", 2 );
					(chg,_G.testfile) = testfilepicker.Property( "Pick a test file", "test file", _G.testfile );
				}
			);
		});
	}
	
	ImGui_Render();
	
	SS_Present();
}

function on_event( e )
{
	if( e.type == SDL_QUIT )
		global sys_exit = true;
	ImGui_SS_Event( e );
}

function cleanup()
{
}
