
function init_tracking()
{
	global tracking_event_list =
	[
		SDL_CONTROLLERAXISMOTION,
		SDL_CONTROLLERBUTTONDOWN,
		SDL_CONTROLLERBUTTONUP,
		SDL_CONTROLLERDEVICEADDED,
		SDL_CONTROLLERDEVICEREMOVED,
		SDL_CONTROLLERDEVICEREMAPPED,
		SDL_DOLLARGESTURE,
		SDL_DROPFILE,
		SDL_FINGERMOTION,
		SDL_FINGERDOWN,
		SDL_FINGERUP,
		SDL_JOYAXISMOTION,
		SDL_JOYBALLMOTION,
		SDL_JOYBUTTONDOWN,
		SDL_JOYBUTTONUP,
		SDL_JOYDEVICEADDED,
		SDL_JOYDEVICEREMOVED,
		SDL_JOYHATMOTION,
		SDL_KEYDOWN,
		SDL_KEYUP,
		SDL_MOUSEBUTTONDOWN,
		SDL_MOUSEBUTTONUP,
		SDL_MOUSEMOTION,
		SDL_MOUSEWHEEL,
		SDL_MULTIGESTURE,
		SDL_TEXTEDITING,
		SDL_TEXTINPUT,
	];
}

function init_joysticks()
{
	global Joysticks = {};
}


function map_global_to_name( vv, pfx )
{
	foreach( key, val : _G )
	{
		if( pfx && string_part( key, 0, pfx.length ) != pfx )
			continue;
		if( vv == val )
			return key;
	}
	return "<unknown(" $ vv $ ")>";
}

function event_to_string( e )
{
	if( e.type == SDL_KEYUP || e.type == SDL_KEYDOWN )
		e.keyname = map_global_to_name( e.keycode, "SDLK_" );
	e.type = map_global_to_name( e.type, "SDL_" );
	outarr = [];
	foreach( k, v : e )
		outarr.push( k $ " = " $ v );
	return string_implode( outarr, " | " );
}

function configure()
{
}

function initialize()
{
	global Window = SS_CreateWindow( "SGS-SDL Game Framework",
		SDL_WINDOWPOS_UNDEFINED, SDL_WINDOWPOS_UNDEFINED, 1024, 576,
		SDL_WINDOW_RESIZABLE | SDL_WINDOW_OPENGLMAYBE );
	Window.initRenderer( SS_RENDERER_DONTCARE, SS_RENDERER_VSYNC );
	global Font = SS_CreateFont( "fonts/cousine-regular.ttf", 10 );
	global Events = [];
	init_tracking();
	init_joysticks();
}

function update()
{
	padding = 24;
	SS_Clear( color(0.05,1) );
	SS_SetCameraUI( 0, Window.width, 0, Window.height );
	
	foreach( i, event : Events )
	{
		SS_DrawTextLine( event, Font, padding, Window.height - padding - (i+1) * Font.size * 1.2, color(0.9,1) );
	}
	
	SS_Present();
}

function on_event( e )
{
	if( e.type == SDL_QUIT )
		global sys_exit = true;
	
	if( e.type == SDL_JOYDEVICEADDED )
	{
		Joysticks[ e.which ] = SS_GetJoystick( e.which );
	}
	else if( e.type == SDL_JOYDEVICEREMOVED )
	{
		unset( Joysticks, e.which );
	}
	
	if( tracking_event_list.find( e.type ) !== null )
	{
		if( Events.size > 100 )
			Events.resize( 100 );
		Events.unshift( event_to_string( e ) );
	}
}

