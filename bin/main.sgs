
include "fmt", "engine/all.sgs";


function configure()
{
	global sys_exit;

	editor = false;
	
	for( i = 0; i < sys_args.size; ++i )
	{
		var arg = sys_args[ i ];
		if( arg == "-preexit" )
		{
			println( "PREEXIT" );
			sys_exit = true;
		}
		else if( arg == "-profile" )
		{
			enable_profiler();
			println( "PROFILER ENABLED" );
		}
		else if( arg == "-profile-ops" )
		{
			enable_profiler2();
			println( "PROFILER [mode 2] ENABLED" );
		}
		else if( arg == "-editor" )
		{
			editor = true;
		}
	}

	if( editor )
	{
		include_file( "editor/main.sgs" );
		configure();
	}
}

function initialize()
{
	set_caption( "The SGS-SDL Game Framework" );
	set_video_mode( 1024, 576, 32, "" );
	
	global UI = UIFrame.create();
//	UI.addChild( UIControl.createButton( "Make it rain!", 10, 10, 120, 40, function(){} ) );

	global TM = Tilemap.create( create_texture( "ntset.png", "nolerp" ), 32, 32 );
	floor = _G.floor;
	randf = _G.randf;
	for( y = 0; y < 64; ++y )
	{
		for( x = 0; x < 64; ++x )
		{
			TM.set( x, y, floor( randf() * 3.99 ), floor( randf() * 3.99 ), [1,1,1] );
		}
	}
	
	println();
	global image = create_texture( "crosshair.png", "mipmaps" );
	println( image );
	printvar( _Gtex );
	println();
}

global mpos = [0,0];

global lasttime = ftime();
global rotation = 0;

function update()
{
	global lasttime, rotation, P;
	var curtime = ftime();
	var delta = curtime - lasttime;
	if( delta > 1.0/15.0 )
		delta = 1.0/15.0;
	lasttime = curtime;
	
	rotation += delta;
	
	var cc = [0.1,0.2,0.4];
	clear( cc );

	set_camera_2d( 500, 500, 500, 1024/576, 0 );

	TM.render();
	
	vdata = fmt_pack( "5f12x 5f12x 5f12x 5f12x",
		100, 100, 0, 0, 0,
		200, 100, 0, 1, 0,
		300, 300, 0, 1, 1,
		100, 200, 0, 0, 1
	);
	vfmt = make_vertex_format( "|pf3tf2" );
	idata = fmt_pack( "4w", 1, 2, 3, 0 );
	draw_packed( image, vfmt, vdata, 0, 4, GL_TRIANGLE_FAN );
	draw_packed( image, vfmt, vdata, 0, 4, GL_TRIANGLE_FAN, idata );

	set_camera_ui( 0, 1024, 0, 576 );

	draw({ preset = "box", texture = image, position = mpos, scale = [128,128], angle = rotation * M_PI });
	
	UI.draw();
	
	fn = create_font( "verdana.ttf", 16 );
	tx = "frame time: " $ ( ftime() - lasttime ) * 1000.0 $ " ms";
	draw_text_line( tx, fn, 301, 1, [0,0,0,1] );
	draw_text_line( tx, fn, 300, 0, [1] );
	
	present();

	sleeptime = 15 - delta * 1000;
	if( sleeptime > 0 ) sleep( sleeptime );
}

function on_event( e )
{
	if( e.type == SDL_QUIT )
		global sys_exit = true;
	if( e.type == SDL_MOUSEMOTION )
		global mpos = [e.x,e.y];

	UI.event( e );
}

function cleanup()
{
}
