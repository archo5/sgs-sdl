

global
	EM_VIEW =  0,
	EM_EDIT =  1,
	EM_PUT =   2,
	EM_PAINT = 3;

global
	EA_NONE = 0,
	EA_MOVE = 1,
	EA_PAINT = 2;


function ED_createLevelState( tfname )
{
	return
	{
		name = tfname,
		objects = [],
		selected = [],
	};
}

function ED_createState()
{
	var deficon = "editor/img/default_icon.png";
	var state =
	{
		mode = EM_VIEW,
		action = EA_NONE,
		origpos = null,
		origcurpos = null,

		view_x = 0,
		view_y = 0,
		view_size = 300,

		mouseon = -1,
		scrolling = false,

		show_grid = true,
		snap_grid = true,
		grid_w = 16,
		grid_h = 16,

		modified = false,
		file = null,
		state = ED_createLevelState( "New Level" ),
		undo_list = [],
		redo_list = [],

		default_icon = deficon,
		default_itex = create_texture( deficon ),
		mouseon_tex = create_texture( "editor/img/mouseon.png" ),
		select_tex = create_texture( "editor/img/select.png" ),
	};

	// additional helper links to current state
	state.objects = state.state.objects;
	state.selected = state.state.selected;
	return state;
}

function ED_addUndoLevel( name )
{
	ED.redo_list.clear();

	cstate = clone( ED.state );
	cstate.name = name;
	cstate.objects = clone( cstate.objects );
	cstate.selected = clone( cstate.selected );

	ED.undo_list.push( cstate );
	ED.modified = true;
	ED_message( name );
}

function ED_createObject( name, x, y, painting )
{
	painting ||= false;

	var data = null;
	if( isset( EDOBJ, name ) )
		data = EDOBJ[ name ];

	obj = { name = name, x = x, y = y, data = data };
	if( obj.data != null )
	{
		obj.tex = obj.data.texture;
		obj.width = obj.data.width;
		obj.height = obj.data.height;
	}
	else
	{
		obj.tex = ED.default_itex;
		obj.width = obj.tex.width;
		obj.height = obj.tex.height;
	}
	if( !painting )
		ED_addUndoLevel( "Added '" $ name $ "'" );
	ED.state.objects.push( obj );
	return obj;
}

function ED_removeObjects( objs )
{
	if( objs.size )
	{
		name = "Deleted ";
		donl = [];
		for( i = 0; i < objs.size && i < 5; ++i )
			donl.push( "'" $ objs[ i ].name $ "'" );
		name $= string_implode( donl, ", " );
		if( objs.size > 5 )
			name $= " and others";
		ED_addUndoLevel( name );
	}
	foreach( obj : objs )
	{
		foreach( i, so : ED.objects )
		{
			if( so === obj )
			{
				ED.objects.erase( i );
				break;
			}
		}
		foreach( i, sel : ED.selected )
		{
			if( sel === obj )
			{
				ED.selected.erase( i );
				break;
			}
		}
	}
}

function ED_loadObjDefs()
{
	var defs = json_decode( io_file_read( "editor/objects.json" ) );
	foreach( key, def : defs )
	{
		def.id = key;
		if( !isset( def, "name" ) ){ def.name = key; }
		if( !isset( def, "icon" ) ){ def.icon = ED.default_icon; }
		if( !isset( def, "origin_x" ) ){ def.origin_x = 0; }
		if( !isset( def, "origin_y" ) ){ def.origin_y = 0; }
		if( !isset( def, "params" ) ){ def.params = []; }

		def.texture = create_texture( def.icon );
		if( !isset( def, "width" ) ){ def.width = def.texture.width; }
		if( !isset( def, "height" ) ){ def.height = def.texture.height; }
	}
	return defs;
}

function ED_initialize()
{
	global ED, EDOBJ, EDOLIST;
	ED = ED_createState();
	EDOBJ = ED_loadObjDefs();
	EDOLIST = [];
	foreach( v : EDOBJ )
		EDOLIST.push( v );
}

function transform_to_view( mtx, x, y, w, h )
{
	pt = multiply_point_matrix( mtx, x, y );
	return vec2d
	(
		( pt.x * 0.5 + 0.5 ) * w,
		( pt.y * -0.5 + 0.5 ) * h
	);
}

function ED_draw()
{
	aspect = UI.w / UI.h;

	mtx = get_camera_2d_mtx( ED.view_x, ED.view_y, ED.view_size, aspect, 0 );

	set_camera( mtx );

	/* OBJECTS */
	foreach( oid, obj : ED.objects )
	{
		x = obj.x;
		y = obj.y;
		tex = obj.tex;
		width = obj.width;
		height = obj.height;
		if( obj.data != null )
		{
			x -= obj.data.origin_x;
			y -= obj.data.origin_y;
		}
		draw({ preset = "tile", texture = tex, position = [ x, y ], scale = [ width, height ] });
	}

	/* GHOST OBJECTS (future) */
	if( ( ED.mode == EM_PUT || ED.mode == EM_PAINT ) && ED.action != EA_MOVE )
	{
		x = ED_getworldx( UI.lmpos_x );
		y = ED_getworldy( UI.lmpos_y );
		if( ED.snap_grid )
		{
			x = ED_snapx( x );
			y = ED_snapy( y );
		}
		obj = EDOLIST[ UI.formdata[ "object_type" ] ];
		x -= obj.origin_x;
		y -= obj.origin_y;
		draw({ preset = "tile", texture = obj.texture, position = [ x, y ],
			scale = [ obj.width, obj.height ], color = [ 1, 1, 1, 0.5 ] });
	}
	
	/* SELECTED / MOUSE-ON */
	if( ED.mode == EM_EDIT )
	{
		set_camera_ui( 0, UI.w, 0, UI.h );
		foreach( oid, obj : ED.selected )
		{
			x1 = obj.x;
			y1 = obj.y;
			if( obj.data != null )
			{
				x1 -= obj.data.origin_x;
				y1 -= obj.data.origin_y;
			}
			x2 = x1 + obj.width;
			y2 = y1 + obj.height;

			p1 = transform_to_view( mtx, x1, y1, UI.w, UI.h );
			p2 = transform_to_view( mtx, x2, y2, UI.w, UI.h );

			draw_button( ED.select_tex, p1.x - 4, p2.x + 4, p1.y - 4, p2.y + 4, 4, 4 );
		}
		if( ED.mouseon != -1 )
		{
			obj = ED.objects[ ED.mouseon ];
			x1 = obj.x;
			y1 = obj.y;
			if( obj.data != null )
			{
				x1 -= obj.data.origin_x;
				y1 -= obj.data.origin_y;
			}
			x2 = x1 + obj.width;
			y2 = y1 + obj.height;

			p1 = transform_to_view( mtx, x1, y1, UI.w, UI.h );
			p2 = transform_to_view( mtx, x2, y2, UI.w, UI.h );

			draw_button( ED.mouseon_tex, p1.x - 4, p2.x + 4, p1.y - 4, p2.y + 4, 4, 4 );
		}
	}

	/* PAINTBRUSH OBSTRUCTIONS */
	if( ED.mode == EM_PAINT )
	{
		set_camera_ui( 0, UI.w, 0, UI.h );
		x = ED_getworldx( UI.lmpos_x );
		y = ED_getworldy( UI.lmpos_y );

		if( ED.snap_grid )
		{
			x = ED_snapx( x );
			y = ED_snapy( y );
		}

		ox = x;
		oy = y;
		obj = EDOLIST[ UI.formdata[ "object_type" ] ];
		x -= obj.origin_x;
		y -= obj.origin_y;
		w = obj.width;
		h = obj.height;

		objs = ED_findObjectsInBox( x, x + w, y, y + h );
		foreach( oid, obj : objs )
		{
			x1 = obj.x;
			y1 = obj.y;
			if( obj.data != null )
			{
				x1 -= obj.data.origin_x;
				y1 -= obj.data.origin_y;
			}
			x2 = x1 + obj.width;
			y2 = y1 + obj.height;

			p1 = transform_to_view( mtx, x1, y1, UI.w, UI.h );
			p2 = transform_to_view( mtx, x2, y2, UI.w, UI.h );

			draw_button( ED.select_tex, p1.x - 4, p2.x + 4, p1.y - 4, p2.y + 4, 4, 4 );
		}
	}

	set_camera( mtx );

	/* THE GRID */
	if( ED.show_grid )
	{
		vsx = ED.view_size * aspect;
		vsy = ED.view_size;
		refx1 = ED_snapx( ED.view_x - vsx - ED.grid_w );
		refy1 = ED_snapy( ED.view_y - vsy - ED.grid_h );
		refx2 = ED_snapx( ED.view_x + vsx + ED.grid_w );
		refy2 = ED_snapy( ED.view_y + vsy + ED.grid_h );

		positions = [];
		positions.reserve( ( refx2 - refx1 ) / ED.grid_w + 1 );
		positions.reserve( ( refy2 - refy1 ) / ED.grid_h + 1 );

		for( x = refx1; x <= refx2; x += ED.grid_w )
			positions.push([ x, ED.view_y ]);
		draw({ preset = "box", positions = positions,
			scale = [ vsy / UI.w * 4, vsy * 2 ], color = [0.9,0.9,0.9,0.5] });

		positions.resize( 0 );
		for( y = refy1; y <= refy2; y += ED.grid_h )
			positions.push([ ED.view_x, y ]);
		draw({ preset = "box", positions = positions,
			scale = [ vsx * 2, vsy / UI.w * 4 ], color = [0.9,0.9,0.9,0.5] });
	}
}

function ED_getworldx( x )
{
	return ( x - UI.w * 0.5 ) / UI.h * ED.view_size * 2 + ED.view_x;
}
function ED_getworldy( y )
{
	return ( y - UI.h * 0.5 ) / UI.h * ED.view_size * 2 + ED.view_y;
}
function ED_snapx( x )
{
	return round( x / ED.grid_w ) * ED.grid_w;
}
function ED_snapy( y )
{
	return round( y / ED.grid_h ) * ED.grid_h;
}

function ED_findObjectsInBox( x1, x2, y1, y2 )
{
	out = [];
	foreach( obj : ED.objects )
	{
		ox1 = obj.x;
		oy1 = obj.y;
		if( obj.data )
		{
			ox1 -= obj.data.origin_x;
			oy1 -= obj.data.origin_y;
		}
		ox2 = ox1 + obj.width;
		oy2 = oy1 + obj.height;

		if( x1 < ox2 && ox1 < x2 &&
			y1 < oy2 && oy1 < y2 )
			out.push( obj );
	}
	return out;
}

function ED_beginPaint()
{
	ED.action = EA_PAINT;
	ED.numpainted = 0;
}
function ED_endPaint()
{
}
function ED_doPaint( x, y )
{
	x = ED_getworldx( x );
	y = ED_getworldy( y );

	if( ED.snap_grid )
	{
		x = ED_snapx( x );
		y = ED_snapy( y );
	}

	ox = x;
	oy = y;
	obj = EDOLIST[ UI.formdata[ "object_type" ] ];
	x -= obj.origin_x;
	y -= obj.origin_y;
	w = obj.width;
	h = obj.height;

	objs = ED_findObjectsInBox( x, x + w, y, y + h );
	if( objs.size == 0 )
	{
		if( ED.numpainted == 0 )
			ED_addUndoLevel( "Painted '" $ obj.name $ "'" );
		ED_createObject( obj.id, ox, oy );
		ED.numpainted++;
	}

	ED_message( "Painting... Number of objects painted: " $ ED.numpainted );
}

function ED_zoomMsg()
{
	zoompc = UI.h / ED.view_size * 50;
	ED_message( "Zoom: " $ round( zoompc * 100 ) / 100 $ "%" );
}

function ED_updatehover( x, y )
{
	x = ED_getworldx( x );
	y = ED_getworldy( y );
	ED.mouseon = -1;
	for( i = ED.objects.size - 1; i >= 0; --i )
	{
		O = ED.objects[ i ];
		ox = O.x;
		oy = O.y;
		if( O.data != null )
		{
			ox -= O.data.origin_x;
			oy -= O.data.origin_y;
		}
		if( x >= ox && y >= oy && x < ox + O.width && y < oy + O.height )
		{
			ED.mouseon = i;
			break;
		}
	}
}

function ED_event( e )
{
	if( e.type == SDL_MOUSEBUTTONDOWN )
	{
		var ctrl = UI.get_ctrl_at( e.x, e.y );
		if( ctrl !== null && ctrl !== UI )
			return;

		q = 0;
		if( e.button == 1 && ED.mode == EM_EDIT )
		{
			if( ED.selected.size && ED.mouseon != -1 &&
				ED.selected[0] && ED.objects[ ED.mouseon ] == ED.selected[0] )
			{
				ED.action = EA_MOVE;
				ED.origpos = vec2d( ED.selected[0].x, ED.selected[0].y );
				ED.origcurpos = vec2d( ED_getworldx( e.x ), ED_getworldy( e.y ) );
			}
			ED.selected = [];
			if( ED.mouseon != -1 )
				ED.selected.push( ED.objects[ ED.mouseon ] );
		}
		if( e.button == 2 )
			ED.scrolling = true;
		if( e.button == 4 )
			q = 0.1;
		if( e.button == 5 )
			q = -0.1;
		if( q )
		{
			if( ( q > 0 && ED.view_size < 10 ) ||
				( q < 0 && ED.view_size > 1000 ) )
			{
				ED_zoomMsg();
				return;
			}

			w = UI.w; if( w <= 0 ) w = 1;
			h = UI.h; if( h <= 0 ) h = 1;
			sz = h;
			x = ( e.x - w * 0.5 ) / sz;
			y = ( e.y - h * 0.5 ) / sz;
			ED.view_x += x * ED.view_size * q * 4;
			ED.view_y += y * ED.view_size * q * 4;
			ED.view_size *= pow( 0.1, q );

			ED_updatehover( e.x, e.y );
			ED_zoomMsg();
		}

		if( ED.mode == EM_PUT && e.button == 1 )
		{
			x = ED_getworldx( e.x );
			y = ED_getworldy( e.y );
			if( ED.snap_grid )
			{
				x = ED_snapx( x );
				y = ED_snapy( y );
			}
			obj = ED_createObject( EDOLIST[ UI.formdata[ "object_type" ] ].id, x, y );
			ED.selected = [obj];
			ED.action = EA_MOVE;
			ED.origpos = ED.origcurpos = vec2d( x, y );
			return x;
		}
		if( ED.mode == EM_PAINT && e.button == 1 )
		{
			ED_beginPaint();
			ED_doPaint( e.x, e.y );
		}
	}
	else if( e.type == SDL_MOUSEBUTTONUP )
	{
		if( e.button == 1 )
		{
			if( ED.action == EA_PAINT )
				ED_endPaint();
			ED.action = EA_NONE;
		}
		if( e.button == 2 )
			ED.scrolling = false;
	}
	else if( e.type == SDL_MOUSEMOTION )
	{
		var ctrl = UI.get_ctrl_at( e.x, e.y );
		if( ctrl !== null && ctrl !== UI )
			return;

		if( ED.scrolling )
		{
			ED.view_x -= e.xrel / UI.h * ED.view_size * 2;
			ED.view_y -= e.yrel / UI.h * ED.view_size * 2;
		//	ED_render();
		}
		ED_updatehover( e.x, e.y );

		if( ED.action == EA_MOVE )
		{
			wx = ED_getworldx( e.x );
			wy = ED_getworldy( e.y );
			ED.selected[0].x = wx - ED.origcurpos.x + ED.origpos.x;
			ED.selected[0].y = wy - ED.origcurpos.y + ED.origpos.y;
			if( ED.snap_grid )
			{
				ED.selected[0].x = ED_snapx( ED.selected[0].x );
				ED.selected[0].y = ED_snapy( ED.selected[0].y );
			}
		}

		if( ED.action == EA_PAINT )
			ED_doPaint( e.x, e.y );
	}
	else if( e.type == SDL_KEYDOWN )
	{
		if( e.keycode == SDLK_DELETE && ED.selected.size )
			ED_removeObjects( clone( ED.selected ) );
	}
}

